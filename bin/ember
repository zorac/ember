#!/usr/bin/env perl

=head1 NAME

ember - A CLI-based reader for eBooks.

=head1 SYNOPSIS

ember my_ebook.epub

=head1 DESCRIPTION

Ember is a very simple CLI-based reader for eBooks. It will automatically adapt
to your terminal width, and remembers the last position in each book.

=cut

use 5.008;
use strict;
use warnings FATAL => 'all', NONFATAL => 'redefine';

use Carp;
use FindBin;
use lib "$FindBin::Bin/../lib";

use Ember::Args;
use Ember::Script;
use Ember::Util qw( get_class );

my $args = Ember::Args->new(@ARGV);
my ($class, $app);

if ($args->{debug}) {
    $Carp::Verbose = 1;
    $SIG{__DIE__} = \&confess;
    $SIG{__WARN__} = \&cluck;
}

eval {
    foreach my $script (@Ember::Script::FOR_ARG) {
        if ($args->{$script->[0]}) {
            $class = get_class('Script', $script->[1]);
        }
    }

    $class = get_class() if (!$class);
    $app = $class->new($args);
    $app->run();
}; if ($@) {
    undef($app); # Make it die!

    if ($@ =~ /^Can't locate \S+ in \@INC \(you may need to install the (\S+) module\)/) {
        print STDERR <<"EOF";

It looks like one or more of Ember's dependencies are missing. In particualar,
Perl can't find the following module: $1

You can install all the modules which Ember requires by using CPAN:

perl -MCPAN -e 'notest install qw( Archive::Zip File::Slurp HTML::Tree JSON Term::ReadKey XML::Simple );

EOF
    } else {
        print STDERR <<"EOF";

Sorry, it looks like Ember has encountered an error. If you'd like to submit a
bug report, you can do so here: https://github.com/zorac/ember/issues
Please include the error dump below, and any details as to what you were doing
at the time.

$@
EOF
    }
}

=head2 Key Commands

=over

=item N, I<Space>

Go to the next page.

=item P, B

Go to the previous page.

=item I

Display book information.

=item C

Display table of contents; type a chapter number and press enter to jump to
that chapter.

=item R

Refresh the screen.

=item H

Display help about the current screen.

=item Q, I<Escape>

Exit to previous screen, or quit Ember.

=back

=head2 Suppported File Formats

=over

=item EPUB

Ember can read EPUB files in both zipped and unpacked formats.

=back

=head1 SEE ALSO

L<Ember>, L<Ember::Reader>

=head1 AUTHOR

Mark Rigby-Jones <mark@rigby-jones.net>

=cut
